plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.3.7.RELEASE'
    id 'idea'
    id 'io.freefair.lombok' version '5.2.1'
}

apply plugin: 'io.spring.dependency-management'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}
sourceSets {
    functionalTest {
        compileClasspath += sourceSets.main.output + configurations.testCompile
        runtimeClasspath += output + compileClasspath + configurations.testRuntime
    }
}

idea {
    module {
        testSourceDirs += sourceSets.functionalTest.java.srcDirs
        testResourceDirs += sourceSets.functionalTest.resources.srcDirs
        scopes.TEST.plus += [ configurations.functionalTestCompile ]
    }
}

configurations {
    functionalTestImplementation.extendsFrom testImplementation
    functionaltestRuntime.extendsFrom testRuntime
    functionalTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task functionalTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    outputs.upToDateWhen { false }
    useJUnitPlatform()
    mustRunAfter test
}

group 'com.personia'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "Hoxton.SR9")
    set('dozerVersion', "6.5.2")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation "com.github.dozermapper:dozer-core:${dozerVersion}"
    implementation "com.github.dozermapper:dozer-spring-boot-starter:${dozerVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    runtimeOnly 'com.h2database:h2'
}

test {
    useJUnitPlatform()
    testLogging {
        events = ['FAILED', 'PASSED', 'STANDARD_OUT']
    }
}

check.dependsOn functionalTest